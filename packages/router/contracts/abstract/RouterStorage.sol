// SPDX-License-Identifier: GPL-2.0-or-later
pragma solidity 0.8.19;

import '@openzeppelin/contracts/access/Ownable2Step.sol';

import '../interfaces/IBlast.sol';
import '../interfaces/IMarginlyAdapter.sol';
import '../interfaces/IMarginlyRouter.sol';

struct AdapterInput {
  uint256 dexIndex;
  address adapter;
}

abstract contract RouterStorage is IMarginlyRouter, Ownable2Step {
  /// @notice Emitted when new adapter is added
  event NewAdapter(uint256 dexIndex, address indexed adapter);

  error UnknownDex();

  IBlast private constant BLAST = IBlast(0x4300000000000000000000000000000000000002);

  mapping(uint256 => address) public adapters;

  constructor(AdapterInput[] memory _adapters) {
    AdapterInput memory input;
    uint256 length = _adapters.length;
    for (uint256 i; i < length; ) {
      input = _adapters[i];
      adapters[input.dexIndex] = input.adapter;
      emit NewAdapter(input.dexIndex, input.adapter);

      unchecked {
        ++i;
      }
    }

    BLAST.configureClaimableGas();
  }

  function addDexAdapters(AdapterInput[] calldata _adapters) external onlyOwner {
    AdapterInput memory input;
    uint256 length = _adapters.length;
    for (uint256 i; i < length; ) {
      input = _adapters[i];
      adapters[input.dexIndex] = input.adapter;
      emit NewAdapter(input.dexIndex, input.adapter);

      unchecked {
        ++i;
      }
    }
  }

  function getAdapterSafe(uint256 dexIndex) internal view returns (IMarginlyAdapter) {
    address adapterAddress = adapters[dexIndex];
    if (adapterAddress == address(0)) revert UnknownDex();
    return IMarginlyAdapter(adapterAddress);
  }

  function claimContractsGas(address feeHolder) external onlyOwner {
    BLAST.claimAllGas(address(this), feeHolder);
  }

  function renounceOwnership() public override onlyOwner {
    revert Forbidden();
  }
}
